plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

ext {
    forge_server_version = '21.1.0'
}

group 'com.bolyartech.forge.server'
version forge_server_version

apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    api 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    api 'com.google.code.gson:gson:2.10'
    api 'javax.inject:javax.inject:1'
    api  group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'
    implementation 'org.slf4j:slf4j-api:2.0.5'
    implementation 'ch.qos.logback:logback-classic:1.4.12'
    implementation group: 'com.google.guava', name: 'guava', version: '33.0.0-jre'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    api group: 'org.apache.velocity', name: 'velocity-engine-core', version: '2.3'
    api group: 'org.apache.velocity.tools', name: 'velocity-tools-generic', version: '3.1'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(11)
}
//tasks.withType(Javadoc).all {
//    enabled = false
//}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.bolyartech.forge.server'
            artifactId 'forge-server'
            version forge_server_version
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Forge Server'
                artifactId = 'forge-server'
                packaging = 'jar'
                description = 'Forge server is the base for the server part of the Forge framework'
                url = 'https://github.com/ogrebgr/forge-server'

                scm {
                    connection = 'scm:git:https://github.com/ogrebgr/forge-server.git'
                    developerConnection = 'scm:git:https://github.com/ogrebgr/forge-server.git'
                    url  = 'https://github.com/ogrebgr/forge-server'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'ogremir'
                        name = 'Ognyan Bankov'
                        email = 'bankov@bolyartech.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
}



signing {
    sign publishing.publications.mavenJava
}

archivesBaseName = "forge-server"

